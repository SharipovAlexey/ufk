
&НаСервере
Процедура ВлажностьПриИзмененииНаСервере()
	Для каждого Строкатч из Объект.ТранспортировочныеМеста Цикл
		Строкатч.ВесНеттоСухой = Окр(Строкатч.ВесНеттоВлажный * ((100-объект.Влажность)/100),0,РежимОкругления.Окр15как20);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВлажностьПриИзменении(Элемент)
	ВлажностьПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ТранспортировочныеМестаВесНеттоВлажныйПриИзменении(Элемент)
	Строкатч = Элементы.ТранспортировочныеМеста.ТекущиеДанные;
	Строкатч.ВесНеттоСухой = Окр(Строкатч.ВесНеттоВлажный * ((100-объект.Влажность)/100),0,РежимОкругления.Окр15как20);	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортировочныеМестаВесБруттоВлажныйПриИзменении(Элемент)
	Строкатч = Элементы.ТранспортировочныеМеста.ТекущиеДанные;
	Строкатч.ВесНеттоВлажный = Строкатч.ВесБруттоВлажный - Объект.ВесЕдиницыТары;
	Строкатч.ВесНеттоСухой = Окр(Строкатч.ВесНеттоВлажный * ((100-объект.Влажность)/100),0,РежимОкругления.Окр15как20);	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	
КонецПроцедуры


// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТранспортировочныеМестаНаКлиенте()
	Перем ВыбЗнач;
	Массив = Новый Массив;
	Массив.Добавить(Тип("Число"));
	КЧ = Новый КвалификаторыЧисла(3,0,ДопустимыйЗнак.Неотрицательный);
	ОписаниеТипов = Новый ОписаниеТипов(Массив, КЧ, , );
	Оповещение =
	    Новый ОписаниеОповещения("ПослеВводаКоличестваГенерируемыхМест", ЭтаФорма, Параметры);
	ПоказатьВводЗначения(Оповещение,
	    ВыбЗнач, "Введите количество мест", ОписаниеТипов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаКоличестваГенерируемыхМест(ВыбЗнач, Параметры) Экспорт
    Если ВыбЗнач <> Неопределено Тогда
   		СформироватьПакетНовыхТранспортировочныхМестНаСервере(ВыбЗнач);
    КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьПакетНовыхТранспортировочныхМестНаСервере(КоличествоМест)
	Для i = 1 По КоличествоМест Цикл 
		НовоеМесто = Справочники.ТранспортировочныеМеста.СоздатьЭлемент();
		НовоеМесто.Записать();
		новаяСтрока = Объект.ТранспортировочныеМеста.Добавить();
		новаяСтрока.ТранспортировочноеМесто = НовоеМесто.Ссылка;
		новаяСтрока.НомерМестаВПартии = новаяСтрока.НомерСтроки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТранспортировочныеМеста(Команда)
	
	Если Объект.ТранспортировочныеМеста.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'При заполнением новые транспортировочные месте будут добавлены к существующим данным. Продолжить?'");
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнениеЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, Заголовок);
	Иначе
		СоздатьТранспортировочныеМестаНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		СоздатьТранспортировочныеМестаНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ВесЕдиницыТарыПриИзмененииНаСервере()
	Для каждого Строкатч из Объект.ТранспортировочныеМеста Цикл
		Строкатч.ВесНеттоВлажный = Строкатч.ВесБруттоВлажный - Объект.ВесЕдиницыТары;
		Строкатч.ВесНеттоСухой = Окр(Строкатч.ВесНеттоВлажный * ((100-объект.Влажность)/100),0,РежимОкругления.Окр15как20);	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВесЕдиницыТарыПриИзменении(Элемент)
	ВесЕдиницыТарыПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ТранспортировочныеМестаСоздатьТрансопртировочныеМеста.Доступность = Не ЭтаФорма.ТолькоПросмотр;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Для Каждого СтрокаТЧ из Объект.ТранспортировочныеМеста Цикл 
		ЭтаФорма.ОтобразитьИзменениеДанных(СтрокаТЧ.ТранспортировочноеМесто,ВидИзмененияДанных.Изменение);
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ТранспортировочныеМестаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда 
		текСтрока = Элементы.ТранспортировочныеМеста.ТекущиеДанные; 
		текСтрока.НомерМестаВПартии = текСтрока.НомерСтроки;
	КонецЕсли;
КонецПроцедуры

