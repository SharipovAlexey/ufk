
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию(ПараметрыСеанса.ТекущийПользователь.Наименование);	
	Склад = Справочники.Склады.СкладПоУмолчанию(ПараметрыСеанса.ТекущийПользователь.Наименование);
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиПродукцииНаСкладе.Очистить();
	Движения.ОстаткиПродукцииНаСкладе.Записать(Истина);
	
	Движения.ПереработкаПродукции.Очистить();
	Движения.ПереработкаПродукции.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиПродукцииНаСкладеОстатки.Организация КАК Организация,
		|	ОстаткиПродукцииНаСкладеОстатки.Склад КАК Склад,
		|	ОстаткиПродукцииНаСкладеОстатки.ПартияПродукции КАК ПартияПродукции,
		|	ОстаткиПродукцииНаСкладеОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиПродукцииНаСкладеОстатки.ТранспортировочноеМесто КАК ТранспортировочноеМесто,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.КоличествоМестОстаток) КАК КоличествоМестОстаток,
		|	ДанныеЗатаркиТранспортировочныхМест.Влажность КАК Влажность,
		|	0 КАК КоличествоСписано,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.ВесБруттоВлажныйОстаток) КАК ВесБруттоВлажныйОстаток,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.ВесНеттоВлажныйОстаток) КАК ВесНеттоВлажныйОстаток,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.ВесНеттоСухойОстаток) КАК ВесНеттоСухойОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиПродукцииНаСкладе.Остатки(
		|			&МоментВремени,
		|			ТранспортировочноеМесто В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ПередачаПродукцииВПереработкуПродукция.ТранспортировочноеМесто КАК ТранспортировочноеМесто
		|					ИЗ
		|						Документ.ПередачаПродукцииВПереработку.Продукция КАК ПередачаПродукцииВПереработкуПродукция
		|					ГДЕ
		|						ПередачаПродукцииВПереработкуПродукция.Ссылка = &СсылкаНаДокумент)
		|				И Организация = &Организация
		|				И Склад = &Склад
		|				И СостояниеХраниния = ЗНАЧЕНИЕ(перечисление.СостоянияХранения.Хранение)) КАК ОстаткиПродукцииНаСкладеОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеЗатаркиТранспортировочныхМест КАК ДанныеЗатаркиТранспортировочныхМест
		|		ПО ОстаткиПродукцииНаСкладеОстатки.ТранспортировочноеМесто = ДанныеЗатаркиТранспортировочныхМест.ТранспортировочноеМесто
		|ГДЕ
		|	ДанныеЗатаркиТранспортировочныхМест.ТранспортировочноеМесто В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ПередачаПродукцииВПереработкуПродукция.ТранспортировочноеМесто КАК ТранспортировочноеМесто
		|			ИЗ
		|				Документ.ПередачаПродукцииВПереработку.Продукция КАК ПередачаПродукцииВПереработкуПродукция
		|			ГДЕ
		|				ПередачаПродукцииВПереработкуПродукция.Ссылка = &СсылкаНаДокумент)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиПродукцииНаСкладеОстатки.Номенклатура,
		|	ОстаткиПродукцииНаСкладеОстатки.Организация,
		|	ОстаткиПродукцииНаСкладеОстатки.ПартияПродукции,
		|	ОстаткиПродукцииНаСкладеОстатки.ТранспортировочноеМесто,
		|	ОстаткиПродукцииНаСкладеОстатки.Склад,
		|	ДанныеЗатаркиТранспортировочныхМест.Влажность
		|
		|ДЛЯ ИЗМЕНЕНИЯ";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("СсылкаНаДокумент", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОстатков = РезультатЗапроса.Выгрузить();
	
	Для каждого СтрокаТаблицы из Продукция Цикл 
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ПартияПродукции", СтрокаТаблицы.ПартияПродукции);
		СтруктураПоиска.Вставить("ТранспортировочноеМесто",СтрокаТаблицы.ТранспортировочноеМесто);
		
		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтрокОстатков.Количество()  = 0 Тогда 
			ТекстСообщения = "В строке №"+Формат(СтрокаТаблицы.НомерСтроки,"ЧГ=")+" не корректно указано транспортировочное место."+ Символы.ПС +
							"По данным учета на складе """+Склад.Наименование+""" не числится транспортировочное место № " + СтрокаТаблицы.ТранспортировочноеМесто + Символы.ПС+
							"из партии продукции № " + СтрокаТаблицы.ПартияПродукции.Код;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "", "Объект", Отказ);
			Отказ = Истина;	
		Иначе
			СтрокаТаблицыОстатков = МассивСтрокОстатков[0];
			
			Если СтрокаТаблицыОстатков.КоличествоМестОстаток >= 1 Тогда
				Движения.ОстаткиПродукцииНаСкладе.Записывать = Истина;
				
				СтрокаТаблицыОстатков.КоличествоМестОстаток = СтрокаТаблицыОстатков.КоличествоМестОстаток - 1;
				СтрокаТаблицыОстатков.КоличествоСписано = СтрокаТаблицыОстатков.КоличествоСписано + 1;
				
				Движение 				= Движения.ОстаткиПродукцииНаСкладе.Добавить();
				Движение.ВидДвижения 	= ВидДвиженияНакопления.Расход;
				Движение.Период 		= Дата;
				Движение.Организация 	= Организация;
				Движение.Склад 			= Склад;
				Движение.ПартияПродукции = СтрокаТаблицы.ПартияПродукции;
				Движение.Номенклатура 	= СтрокаТаблицы.ПартияПродукции.Номенклатура;
				Движение.ТранспортировочноеМесто = СтрокаТаблицы.ТранспортировочноеМесто;
				Движение.СостояниеХраниния = Перечисления.СостоянияХранения.Хранение;
				Движение.Количество 		= СтрокаТаблицыОстатков.КоличествоОстаток;
				Движение.КоличествоМест 	= 1;
				Движение.ВесНеттоСухой		= СтрокаТаблицыОстатков.ВесНеттоСухойОстаток;
				Движение.ВесНеттоВлажный	= СтрокаТаблицыОстатков.ВесНеттоВлажныйОстаток;
				Движение.ВесБруттоВлажный	= СтрокаТаблицыОстатков.ВесБруттоВлажныйОстаток;
				
				//ПЕРЕРАБОТКА ПРОДУКЦИИ
				Движение 							= Движения.ПереработкаПродукции.Добавить();
				Движение.Период 					= Дата;
				Движение.Организация 				= Организация;
				Движение.Смена		 				= СменаРаботы;
				Движение.Номенклатура 				= СтрокаТаблицы.ПартияПродукции.Номенклатура;
				Движение.ПартияПродукции		 	= СтрокаТаблицы.ПартияПродукции;
				Движение.ТранспортировочноеМесто 	= СтрокаТаблицы.ТранспортировочноеМесто;
				Движение.ВесБруттоВлажный 			= СтрокаТаблицыОстатков.ВесБруттоВлажныйОстаток;
				Движение.ВесНеттоВлажный 			= СтрокаТаблицыОстатков.ВесНеттоВлажныйОстаток;
				Движение.ВесНеттоСухой 				= СтрокаТаблицыОстатков.ВесНеттоСухойОстаток;
				
			Иначе
				ТекстСообщения = "В строке №"+Формат(СтрокаТаблицы.НомерСтроки,"ЧГ=")+" не корректно указано транспортировочное место."+ Символы.ПС +
								"По данным учета на складе """+Склад.Наименование+""" не числится транспортировочное место № " + СтрокаТаблицы.ТранспортировочноеМесто + Символы.ПС+
								"из партии продукции № " + СтрокаТаблицы.ПартияПродукции.Код;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "", "Объект", Отказ);
				Отказ = Истина;	
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры



