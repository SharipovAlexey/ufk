
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ОстаткиПродукцииНаСкладе.Очистить();
	Движения.ОстаткиПродукцииНаСкладе.Записать(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиПродукцииНаСкладеОстатки.Организация КАК Организация,
		|	ОстаткиПродукцииНаСкладеОстатки.Склад КАК Склад,
		|	ОстаткиПродукцииНаСкладеОстатки.ПартияПродукции КАК ПартияПродукции,
		|	ОстаткиПродукцииНаСкладеОстатки.Номенклатура КАК Номенклатура,
		|	ОстаткиПродукцииНаСкладеОстатки.ТранспортировочноеМесто КАК ТранспортировочноеМесто,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.КоличествоОстаток) КАК КоличествоОстаток,
		|	ДанныеЗатаркиТранспортировочныхМест.Влажность КАК Влажность,
		|	ОстаткиПродукцииНаСкладеОстатки.КоличествоМестОстаток КАК КоличествоМестОстаток,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.ВесНеттоСухойОстаток) КАК ВесНеттоСухойОстаток,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.ВесНеттоВлажныйОстаток) КАК ВесНеттоВлажныйОстаток,
		|	СУММА(ОстаткиПродукцииНаСкладеОстатки.ВесБруттоВлажныйОстаток) КАК ВесБруттоВлажныйОстаток,
		|	СУММА(0) КАК КоличествоСписано
		|ИЗ
		|	РегистрНакопления.ОстаткиПродукцииНаСкладе.Остатки(
		|			&МоментВремени,
		|			ТранспортировочноеМесто В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						ОтгрузкаПродукции.ТранспортировочноеМесто КАК ТранспортировочноеМесто
		|					ИЗ
		|						Документ.ТранспортировкаПродукции.Продукция КАК ОтгрузкаПродукции
		|					ГДЕ
		|						ОтгрузкаПродукции.Ссылка = &СсылкаНаДокументПеремещение)
		|				И Организация = &Организация
		|				И Склад = &Склад
		|				И СостояниеХраниния = ЗНАЧЕНИЕ(перечисление.СостоянияХранения.Хранение)) КАК ОстаткиПродукцииНаСкладеОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеЗатаркиТранспортировочныхМест КАК ДанныеЗатаркиТранспортировочныхМест
		|		ПО ОстаткиПродукцииНаСкладеОстатки.ТранспортировочноеМесто = ДанныеЗатаркиТранспортировочныхМест.ТранспортировочноеМесто
		|ГДЕ
		|	ДанныеЗатаркиТранспортировочныхМест.ТранспортировочноеМесто В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ОтгрузкаПродукции.ТранспортировочноеМесто КАК ТранспортировочноеМесто
		|			ИЗ
		|				Документ.ТранспортировкаПродукции.Продукция КАК ОтгрузкаПродукции
		|			ГДЕ
		|				ОтгрузкаПродукции.Ссылка = &СсылкаНаДокументПеремещение)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиПродукцииНаСкладеОстатки.Номенклатура,
		|	ОстаткиПродукцииНаСкладеОстатки.Организация,
		|	ОстаткиПродукцииНаСкладеОстатки.ПартияПродукции,
		|	ОстаткиПродукцииНаСкладеОстатки.ТранспортировочноеМесто,
		|	ОстаткиПродукцииНаСкладеОстатки.Склад,
		|	ДанныеЗатаркиТранспортировочныхМест.Влажность,
		|	ОстаткиПродукцииНаСкладеОстатки.КоличествоМестОстаток
		|
		|ДЛЯ ИЗМЕНЕНИЯ";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("СсылкаНаДокументПеремещение", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОстатков = РезультатЗапроса.Выгрузить();
	
	Для каждого СтрокаТаблицы из Продукция Цикл 
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ПартияПродукции", СтрокаТаблицы.ПартияПродукции);
		СтруктураПоиска.Вставить("ТранспортировочноеМесто",СтрокаТаблицы.ТранспортировочноеМесто);
		
		МассивСтрокОстатков = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
		
		Если МассивСтрокОстатков.Количество()  = 0 Тогда 
			ТекстСообщения = "В строке №"+Формат(СтрокаТаблицы.НомерСтроки,"ЧГ=")+" не корректно указано транспортировочное место."+ Символы.ПС +
							"По данным учета на складе """+Склад.Наименование+""" не числится транспортировочное место № " + СтрокаТаблицы.ТранспортировочноеМесто + Символы.ПС+
							"из партии продукции № " + СтрокаТаблицы.ПартияПродукции.Код;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "", "Объект", Отказ);
			Отказ = Истина;	
		Иначе
			СтрокаТаблицыОстатков = МассивСтрокОстатков[0];
		
			Если СтрокаТаблицыОстатков.КоличествоМестОстаток >= 1 Тогда
				СтрокаТаблицыОстатков.КоличествоМестОстаток = СтрокаТаблицыОстатков.КоличествоМестОстаток - 1;
				СтрокаТаблицыОстатков.КоличествоСписано = СтрокаТаблицыОстатков.КоличествоСписано + 1;
	
				Движения.ОстаткиПродукцииНаСкладе.Записывать = Истина;			
				Движения.ПродукцияВПути.Записывать = Истина;			
			
				Движение = Движения.ОстаткиПродукцииНаСкладе.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.Склад = Склад;
				Движение.ПартияПродукции = СтрокаТаблицы.ПартияПродукции;
				Движение.Номенклатура = СтрокаТаблицы.ПартияПродукции.Номенклатура;
				Движение.ТранспортировочноеМесто = СтрокаТаблицы.ТранспортировочноеМесто;
				Движение.СостояниеХраниния = Перечисления.СостоянияХранения.Хранение;
				
				Движение.Количество 		= СтрокаТаблицыОстатков.КоличествоОстаток;
				Движение.ВесНеттоСухой 		= СтрокаТаблицыОстатков.ВесНеттоСухойОстаток;
				Движение.ВесНеттоВлажный 	= СтрокаТаблицыОстатков.ВесНеттоВлажныйОстаток;
				Движение.ВесБруттоВлажный	= СтрокаТаблицыОстатков.ВесБруттоВлажныйОстаток;
				Движение.КоличествоМест 	= 1;
			
				Движение = Движения.ОстаткиПродукцииНаСкладе.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.Склад = Склад;
				Движение.ПартияПродукции = СтрокаТаблицы.ПартияПродукции;
				Движение.Номенклатура = СтрокаТаблицы.ПартияПродукции.Номенклатура;
				Движение.ТранспортировочноеМесто = СтрокаТаблицы.ТранспортировочноеМесто;
				Движение.СостояниеХраниния = Перечисления.СостоянияХранения.Транспортировка;
				Движение.Количество 		= СтрокаТаблицыОстатков.КоличествоОстаток;
				Движение.ВесНеттоСухой 		= СтрокаТаблицыОстатков.ВесНеттоСухойОстаток;
				Движение.ВесНеттоВлажный 	= СтрокаТаблицыОстатков.ВесНеттоВлажныйОстаток;
				Движение.ВесБруттоВлажный	= СтрокаТаблицыОстатков.ВесБруттоВлажныйОстаток;
			    Движение.КоличествоМест 	= 1;

			
				Движение = Движения.ПродукцияВПути.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.СкладОтправитель = Склад;
				Движение.СкладПолучатель = СкладПолучатель;
				Движение.ПартияПродукции = СтрокаТаблицы.ПартияПродукции;
				Движение.Номенклатура = СтрокаТаблицы.ПартияПродукции.Номенклатура;
				Движение.ТранспортноеСредство = ТранспортноеСредство;
				Движение.ТранспортировочноеМесто = СтрокаТаблицы.ТранспортировочноеМесто;
				Движение.Перевозчик = Грузоперевозчик;
				Движение.Количество 		= СтрокаТаблицыОстатков.КоличествоОстаток;
				Движение.КоличествоМест 	= 1;
				Движение.ВесНеттоСухой 		= СтрокаТаблицыОстатков.ВесНеттоСухойОстаток;
				Движение.ВесНеттоВлажный 	= СтрокаТаблицыОстатков.ВесНеттоВлажныйОстаток;
				Движение.ВесБруттоВлажный	= СтрокаТаблицыОстатков.ВесБруттоВлажныйОстаток;

			Иначе
				ТекстСообщения = "В строке №"+Формат(СтрокаТаблицы.НомерСтроки,"ЧГ=")+" не корректно указано транспортировочное место."+ Символы.ПС +
								"По данным учета на складе """+Склад.Наименование+""" не числится транспортировочное место № " + СтрокаТаблицы.ТранспортировочноеМесто + Символы.ПС+
								"из партии продукции № " + СтрокаТаблицы.ПартияПродукции.Код;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "", "Объект", Отказ);
				Отказ = Истина;	
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию(ПараметрыСеанса.ТекущийПользователь.Наименование);	
	Склад = Справочники.Склады.СкладПоУмолчанию(ПараметрыСеанса.ТекущийПользователь.Наименование);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеПродукцииИзТранспортировки") Тогда
		// Заполнение шапки
		комментарий = ДанныеЗаполнения.комментарий;
		Организация = ДанныеЗаполнения.Организация;
		Склад = ДанныеЗаполнения.СкладПолучатель;
		Для Каждого ТекСтрокаПродукция Из ДанныеЗаполнения.Продукция Цикл
			НоваяСтрока = Продукция.Добавить();
			НоваяСтрока.ВесБруттоВлажный = ТекСтрокаПродукция.ВесБруттоВлажный;
			НоваяСтрока.ВесНеттоВлажный = ТекСтрокаПродукция.ВесНеттоВлажный;
			НоваяСтрока.НомерМестаВПартии = ТекСтрокаПродукция.НомерМестаВПартии;
			НоваяСтрока.НомерПломбы = ТекСтрокаПродукция.НомерПломбы;
			НоваяСтрока.ПартияПродукции = ТекСтрокаПродукция.ПартияПродукции;
			НоваяСтрока.ТранспортировочноеМесто = ТекСтрокаПродукция.ТранспортировочноеМесто;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ОбщийВес = Продукция.Итог("ВесБруттоВлажный");
	
КонецПроцедуры
