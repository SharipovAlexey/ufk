#Область ПрограммныйИнтерфейс
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТТН";
	КомандаПечати.Представление = НСтр("ru = 'Товарно-транспортная накладная'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок       = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная";
	КомандаПечати.Представление = НСтр("ru = 'Накладная ведомость'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок       = 11;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная_ЖД";
	КомандаПечати.Представление = НСтр("ru = 'Накладная ведомость отгрузки ЖД транспортом'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок       = 12;

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Накладная_ЖД_Внешн";
	КомандаПечати.Представление = НСтр("ru = 'Накладная ведомость отгрузки ЖД транспортом (реализация)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок       = 13;
	
	
КонецПроцедуры
#КонецОбласти

Функция НаименованиеСотрудникаДляВизы(Сотрудник)
	
	Если СтрДлина(СокрЛП(Сотрудник.ПредставлениеДляПечати))>0 Тогда
		Возврат СокрЛП(Сотрудник.ПредставлениеДляПечати);
	Иначе 
		Возврат СокрЛП(Сотрудник.Наименование);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция СтруктураИтоговыеСуммыТТН()
	
	Структура = Новый Структура;
	
	// Инициализация итогов по странице.
	Структура.Вставить("ИтогоМассаБруттоНаСтранице", 0);
	Структура.Вставить("ИтогоМестНаСтранице", 0);
	Структура.Вставить("ИтогоКоличествоНаСтранице", 0);
	Структура.Вставить("ИтогоСуммаНаСтранице", 0);
	Структура.Вставить("ИтогоМассаНеттоНаСтранице", 0);
	
	// Инициализация итогов по документу.
	Структура.Вставить("ИтогоМассаБрутто", 0);
	Структура.Вставить("ИтогоМест", 0);
	Структура.Вставить("ИтогоКоличество", 0);
	Структура.Вставить("ИтогоСумма", 0);
	Структура.Вставить("ИтогоМассаНетто", 0);
	
	Структура.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", 0);
	Структура.Вставить("СуммаПрописью", "");
	
	Возврат Структура;
	
КонецФункции // СтруктураИтоговыеСуммы()

Функция СтруктураДанныеСтрокиТТН(КоэффициентПересчета)
	
	Структура = Новый Структура;
	Структура.Вставить("Номер", 0);
	Структура.Вставить("Мест", 0);
	Структура.Вставить("Количество", 0);
	Структура.Вставить("Цена", 0);
	Структура.Вставить("Сумма", 0);
	Структура.Вставить("КоэффициентПересчета", КоэффициентПересчета);
	Структура.Вставить("МассаБрутто", 0);
	Структура.Вставить("МассаНетто", 0);
	
	Возврат Структура;
	
КонецФункции // СтруктураДанныеСтроки()

Процедура РассчитатьИтоговыеСуммыТТН(ИтоговыеСуммы, ДанныеСтроки)
	
	// Увеличим итоги по странице.
	ИтоговыеСуммы.ИтогоМестНаСтранице        = ИтоговыеСуммы.ИтогоМестНаСтранице        + ДанныеСтроки.Мест;
	ИтоговыеСуммы.ИтогоКоличествоНаСтранице  = ИтоговыеСуммы.ИтогоКоличествоНаСтранице  + ДанныеСтроки.Количество;
	ИтоговыеСуммы.ИтогоСуммаНаСтранице       = ИтоговыеСуммы.ИтогоСуммаНаСтранице       + ДанныеСтроки.Сумма;
	ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице = ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице + ДанныеСтроки.МассаБрутто;
	ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  = ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  + ДанныеСтроки.МассаНетто;
	
	// Увеличим итоги по документу.
	ИтоговыеСуммы.ИтогоМест        = ИтоговыеСуммы.ИтогоМест        + ДанныеСтроки.Мест;
	ИтоговыеСуммы.ИтогоКоличество  = ИтоговыеСуммы.ИтогоКоличество  + ДанныеСтроки.Количество;
	ИтоговыеСуммы.ИтогоСумма       = ИтоговыеСуммы.ИтогоСумма       + ДанныеСтроки.Сумма;
	ИтоговыеСуммы.ИтогоМассаБрутто = ИтоговыеСуммы.ИтогоМассаБрутто + ДанныеСтроки.МассаБрутто;
	ИтоговыеСуммы.ИтогоМассаНетто  = ИтоговыеСуммы.ИтогоМассаНетто  + ДанныеСтроки.МассаНетто;
	
КонецПроцедуры // РассчитатьИтоговыеСуммы()

Процедура ОбнулитьИтогиПоСтраницеТТН(ИтоговыеСуммы)
	
	ИтоговыеСуммы.ИтогоМассаБруттоНаСтранице = 0;
	ИтоговыеСуммы.ИтогоМассаНеттоНаСтранице  = 0;
	ИтоговыеСуммы.ИтогоМестНаСтранице        = 0;
	ИтоговыеСуммы.ИтогоКоличествоНаСтранице  = 0;
	ИтоговыеСуммы.ИтогоСуммаНаСтранице       = 0;
	
КонецПроцедуры // ОбнулитьИтогиПоСтранице()

Процедура ЗаполнитьРеквизитыПодвалаТТН(ДанныеПечати, ИтоговыеСуммы, Макет, ТабличныйДокумент,КоличествоНаименований,ЕдиницаИзмеренияВеса = Неопределено, КоэффициентПересчетаВТонны = 0)
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	
	ПараметрыМакета = Новый Структура;
	
	ПолнаяДатаДокумента	= Формат(ДанныеПечати.Дата, "ДФ=""дд ММММ гггг """"года""""""");
	ДлинаСтроки 		= СтрДлина(ПолнаяДатаДокумента);
	ПервыйРазделитель 	= СтрНайти(ПолнаяДатаДокумента, " ");
	ВторойРазделитель 	= СтрНайти(Прав(ПолнаяДатаДокумента, ДлинаСтроки - ПервыйРазделитель), " ") + ПервыйРазделитель;
	
	ПараметрыМакета.Вставить("ДатаДокументаДень",  """" + Лев(ПолнаяДатаДокумента, ПервыйРазделитель -1 ) + """");
	ПараметрыМакета.Вставить("ДатаДокументаМесяц", Сред(ПолнаяДатаДокумента, ПервыйРазделитель + 1, ВторойРазделитель - ПервыйРазделитель - 1));
	ПараметрыМакета.Вставить("ДатаДокументаГод",   Прав(ПолнаяДатаДокумента, ДлинаСтроки - ВторойРазделитель));
	
	ПараметрыМакета.Вставить("ФИОГлавБухгалтера", 		"");
	ПараметрыМакета.Вставить("ФИОРуководителя",   		"");
	
	ПараметрыМакета.Вставить("ДолжностьРуководителя", 	"");
	
	ПараметрыМакета.Вставить("ФИОКладовщика",       	НаименованиеСотрудникаДляВизы(ДанныеПечати.ВизаОтправителя));
	ПараметрыМакета.Вставить("ДолжностьКладовщика", 	ДанныеПечати.ВизаОтправителя.Должность.Наименование);
		
	// Доверенность
	ПараметрыМакета.Вставить("ДоверенностьНомер",     "");
	ПараметрыМакета.Вставить("ДоверенностьДата",      "");
	ПараметрыМакета.Вставить("ДоверенностьВыдана",    "");
	ПараметрыМакета.Вставить("ДоверенностьЧерезКого", "");
	
	Если ИтоговыеСуммы.ИтогоМест > 0 Тогда
		ПараметрыМакета.Вставить("ВсегоМестПрописью", ЧислоПрописью(ИтоговыеСуммы.ИтогоМест, ,",,,,,,,,0"));
	КонецЕсли;
	
	Если ИтоговыеСуммы.ИтогоМассаБрутто > 0 Тогда
		ПараметрыМакета.Вставить("МассаГрузаБуттоПрописью", ЧислоПрописью(ИтоговыеСуммы.ИтогоМассаБрутто, ,",,,,,,,,0")+ " Т.");
		ПараметрыМакета.Вставить("МассаГрузаБрутто", ИтоговыеСуммы.ИтогоМассаБрутто);
	КонецЕсли;
	
	Если ИтоговыеСуммы.ИтогоМассаНетто > 0 Тогда
		ПараметрыМакета.Вставить("МассаГрузаНеттоПрописью", ЧислоПрописью(ИтоговыеСуммы.ИтогоМассаНетто, ,",,,,,,,,0")+ " Т.");
		ПараметрыМакета.Вставить("МассаГрузаНетто", ИтоговыеСуммы.ИтогоМассаНетто);
	КонецЕсли;
	
	ПараметрыМакета.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", ИтоговыеСуммы.КоличествоПорядковыхНомеровЗаписейПрописью);
	ПараметрыМакета.Вставить("ВсегоНаименованийПрописью", ЧислоПрописью(КоличествоНаименований, ,",,,,,,,,0"));
	ПараметрыМакета.Вставить("СуммаПрописью", ИтоговыеСуммы.СуммаПрописью);
	
	ПараметрыМакета.Вставить("ФИОГрузПринял", ДанныеПечати.Водитель);
	
	ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
КонецПроцедуры // ЗаполнитьРеквизитыПодвалаТТН()

Процедура ЗаполнитьРеквизитыТранспортногоРазделаТТН(ДанныеПечати, Макет, ОбластьМакета)
	
	ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
	
	ПараметрыМакета = Новый Структура;
	
	ПараметрыМакета.Вставить("НомерДокумента", ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеПечати.Номер, Ложь, Ложь));
	
	ПараметрыМакета.Вставить("ПредставлениеПеревозчика", pm_ОбщегоНазначения.ОписаниеОрганизации(ДанныеПечати.Грузоперевозчик,"НаименованиеПолное,ЮридическийАдрес"));
	
	ПараметрыМакета.Вставить("МаркаАвтомобиля",       ДанныеПечати.ТранспортноеСредство.МодельТС.Наименование);
	ПараметрыМакета.Вставить("ГосНомерАвтомобиля",    ДанныеПечати.ТранспортноеСредство.НомерТС);
	ПараметрыМакета.Вставить("ПредставлениеВодителя", ДанныеПечати.Водитель);
	ПараметрыМакета.Вставить("ВодительскоеУдостоверение", "");
	ПараметрыМакета.Вставить("ПунктПогрузки",  ДанныеПечати.Склад.Наименование);
	ПараметрыМакета.Вставить("ПунктРазгрузки", ДанныеПечати.СкладПолучатель.Наименование);
	
	ПараметрыМакета.Вставить("ПредставлениеЗаказчикаПеревозок", pm_ОбщегоНазначения.ОписаниеОрганизации(ДанныеПечати.Грузополучатель,"НаименованиеПолное,ЮридическийАдрес"));
	
	ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
	
	СтандартнаяКарточка  = Истина;
	ОграниченнаяКарточка = Ложь;
	
	Если СтандартнаяКарточка
		Или ОграниченнаяКарточка Тогда
		ШрифтСтандарт   = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , ,Не СтандартнаяКарточка);
		ШрифтОграничено = Новый Шрифт(ОбластьМакета.Области.Стандарт.Шрифт, , , , , ,Не ОграниченнаяКарточка);
	КонецЕсли;
	
	ОбластьМакета.Области.Стандарт.Шрифт   = ШрифтСтандарт;
	ОбластьМакета.Области.Ограничено.Шрифт = ШрифтОграничено;
	
КонецПроцедуры

Процедура ДобавитьИтоговыеДанныеПодвалаТТН(ИтоговыеСуммы, ВсегоНомеров)
	
	ИтоговыеСуммы.Вставить("КоличествоПорядковыхНомеровЗаписейПрописью", ЧислоПрописью(ВсегоНомеров, ,",,,,,,,,0"));
	ИтоговыеСуммы.Вставить("СуммаПрописью", "");
	
КонецПроцедуры // ДобавитьИтоговыеДанныеПодвала()

Процедура ЗаполнитьРеквизитыСведенийОГрузе(ДанныеПечати, Макет, ОбластьМакета, ИтоговыеСуммы)
	
	ПараметрыМакета = Новый Структура;
	
	ПараметрыМакета.Вставить("КраткоеНаименованиеГруза",  "ФЛОТОКОНЦЕНТРАТ");
	ПараметрыМакета.Вставить("СпороводительныеДокументы", "ТОВАРНАЯ НАКЛАДНАЯ");
	ПараметрыМакета.Вставить("ВидУпаковкиСведения", "БИГ-БЭГ");
	ПараметрыМакета.Вставить("КолМестСведения", ИтоговыеСуммы.ИтогоМест);
	ПараметрыМакета.Вставить("МассаБруттоСведения", ИтоговыеСуммы.ИтогоМассаБрутто);
	
	ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыПодвалаСведенийОГрузе(ДанныеПечати, Макет, ОбластьМакета)
	
	ПараметрыМакета = Новый Структура;
	
	ПараметрыМакета.Вставить("ФИОКладовщика",       	НаименованиеСотрудникаДляВизы(ДанныеПечати.ВизаОтправителя));
	ПараметрыМакета.Вставить("ДолжностьКладовщика", 	ДанныеПечати.ВизаОтправителя.Должность.Наименование);
			
	ПараметрыМакета.Вставить("Водитель", ДанныеПечати.Водитель);
	
	ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТТН") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ТТН", "Товарно-транспортная накладная",
			СформироватьПечатнуюФормуТТН(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,);
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная") Тогда 
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная", "Накладная ведомость",
			СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,);
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная_ЖД") Тогда 
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная_ЖД", "Накладная ведомость отгрузки ЖД транспортом",
			СформироватьПечатнуюФормуНакладнаяЖД(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,);
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Накладная_ЖД_Внешн") Тогда 
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Накладная_ЖД_Внешн", "Накладная ведомость отгрузки ЖД транспортом (реализация)",
			СформироватьПечатнуюФормуНакладнаяЖД_Реализация(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуТТН(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент 							= Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерКолонтитулаСверху 	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу 	= 0;
	ТабличныйДокумент.ОриентацияСтраницы 		= ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ДвусторонняяПечать 		= ТипДвустороннейПечати.ПереворотВверх;
	ТабличныйДокумент.ПолеСверху				= 5;
	ТабличныйДокумент.ПолеСнизу					= 5;	
	ТабличныйДокумент.АвтоМасштаб 				= Истина;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ТТН";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТранспортировкаПродукции.ПФ_MXL_ТТН");
	
	ОбластьШапка					= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовокТаблицы			= Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьМакета                	= Макет.ПолучитьОбласть("Строка");
	ОбластьИтоговПоСтранице      	= Макет.ПолучитьОбласть("ИтогоПоСтранице");
	ОбластьВсего                 	= Макет.ПолучитьОбласть("Всего");
	ОбластьПодвала               	= Макет.ПолучитьОбласть("Подвал");
	ОбластьТранспортногоРаздела  	= Макет.ПолучитьОбласть("ТранспортныйРаздел");
	ОбластьСведенийОГрузе        	= Макет.ПолучитьОбласть("СведенияОГрузе");
	ОбластьПодвалаСведенийОГрузе 	= Макет.ПолучитьОбласть("ПодвалСведенийОГрузе");
	ОбластьПрочихСведений        	= Макет.ПолучитьОбласть("ПрочиеСведения");
	ОбластьПогрузочныеОперации	 	= Макет.ПолучитьОбласть("ПогрузочныеОперации");

	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("НомерДокумента",					ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СсылкаНаОбъект.Номер, Истина, Ложь));
		СтруктураДанныхДляПечати.Вставить("ДатаДокумента",					СсылкаНаОбъект.Дата);
		СтруктураДанныхДляПечати.Вставить("ОрганизацияПоОКПО",				СсылкаНаОбъект.Организация.КодПоОКПО);	
		СтруктураДанныхДляПечати.Вставить("ГрузополучательПоОКПО",			СсылкаНаОбъект.Грузополучатель.КодПоОКПО);
		СтруктураДанныхДляПечати.Вставить("ПлательщикПоОКПО",				СсылкаНаОбъект.Организация.КодПоОКПО);
		
		ПредставлениеОрганизацииПлательщика = pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Организация,"НаименованиеПолное,ЮридическийАдрес");
		СтруктураДанныхДляПечати.Вставить("ПредставлениеОрганизации",		ПредставлениеОрганизацииПлательщика);
		СтруктураДанныхДляПечати.Вставить("ПредставлениеПлательщика",		ПредставлениеОрганизацииПлательщика);	
		СтруктураДанныхДляПечати.Вставить("ПредставлениеГрузополучателя",	pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Грузополучатель,"НаименованиеПолное,ЮридическийАдрес"));
				
		ОбластьШапка.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		НомерСтраницы 	= 1;
		ИтоговыеСуммы 	= СтруктураИтоговыеСуммыТТН();
		ДанныеСтроки 	= СтруктураДанныеСтрокиТТН(1);
		
		// Создаем массив для проверки вывода
		МассивВыводимыхОбластей = Новый Массив;

		ТабличнаяЧасть = СсылкаНаОбъект.Продукция;
				
		СводнаяТаблицаНоменклатуры = Новый ТаблицаЗначений;
		СводнаяТаблицаНоменклатуры.Колонки.Добавить("Номенклатура");
		СводнаяТаблицаНоменклатуры.Колонки.Добавить("ВесБруттоВлажный",	ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
		СводнаяТаблицаНоменклатуры.Колонки.Добавить("ВесНеттоВлажный", 	ОбщегоНазначения.ОписаниеТипаЧисло(15,3));
		СводнаяТаблицаНоменклатуры.Колонки.Добавить("КоличествоМест", 	ОбщегоНазначения.ОписаниеТипаЧисло(5));
		
		Для Каждого СтрокаТаб Из ТабличнаяЧасть Цикл
			
			ТекущаяПартия = СтрокаТаб.ПартияПродукции;
			
			НоваяСтрокаСводной 					= СводнаяТаблицаНоменклатуры.Добавить();
			НоваяСтрокаСводной.Номенклатура 	= ТекущаяПартия.Номенклатура;
			НоваяСтрокаСводной.ВесБруттоВлажный = СтрокаТаб.ВесБруттоВлажный;
			НоваяСтрокаСводной.ВесНеттоВлажный	= СтрокаТаб.ВесНеттоВлажный;
			НоваяСтрокаСводной.КоличествоМест	= 1;
						
		КонецЦикла;
		
		СводнаяТаблицаНоменклатуры.Свернуть("Номенклатура","ВесБруттоВлажный,ВесНеттоВлажный,КоличествоМест");
		КоличествоСтрок = СводнаяТаблицаНоменклатуры.Количество();

		Для Каждого СтрокаСводной Из СводнаяТаблицаНоменклатуры Цикл
			
			ДанныеСтроки.Номер = ДанныеСтроки.Номер + 1;
			
			ПараметрыМакета = Новый Структура;
			ПараметрыМакета.Вставить("ТоварНаименование",	СтрокаСводной.Номенклатура.ПолноеНаименование);
			ПараметрыМакета.Вставить("ТоварКод", 			СтрокаСводной.Номенклатура.Код);
			ПараметрыМакета.Вставить("Артикул",  			"");

			ДанныеСтроки.МассаБрутто = Окр(СтрокаСводной.ВесБруттоВлажный/1000,3,РежимОкругления.Окр15как20);
			ДанныеСтроки.МассаНетто  = Окр(СтрокаСводной.ВесНеттоВлажный/1000,3,РежимОкругления.Окр15как20);
			
			ДанныеСтроки.Сумма	= 0;
			ДанныеСтроки.Цена	= 0;
			
			ДанныеСтроки.Мест 		= СтрокаСводной.КоличествоМест;
			ДанныеСтроки.Количество	= СтрокаСводной.КоличествоМест;
			
			ПараметрыМакета.Вставить("Цена",  						ДанныеСтроки.Цена);
			ПараметрыМакета.Вставить("Сумма", 						ДанныеСтроки.Сумма);
			ПараметрыМакета.Вставить("Количество", 					ДанныеСтроки.Количество);
			ПараметрыМакета.Вставить("КоличествоМест", 				ДанныеСтроки.Мест);
			ПараметрыМакета.Вставить("БазоваяЕдиницаНаименование",	"Т");
			ПараметрыМакета.Вставить("ВидУпаковки", 				"БИГ-БЭГ");
			ПараметрыМакета.Вставить("МассаНетто", 					ДанныеСтроки.МассаБрутто);

			ОбластьМакета.Параметры.Заполнить(ПараметрыМакета);
			
			Если ДанныеСтроки.Номер = 1 Тогда // первая строка
			    ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы; 
				ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
			Иначе
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьМакета);
				МассивВыводимыхОбластей.Добавить(ОбластьИтоговПоСтранице);
				
				Если ДанныеСтроки.Номер = КоличествоСтрок Тогда
					МассивВыводимыхОбластей.Добавить(ОбластьВсего);
					МассивВыводимыхОбластей.Добавить(ОбластьПодвала);
				КонецЕсли;
				
				Если ДанныеСтроки.Номер <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
					ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
					
					// Очистим итоги по странице.
					ОбнулитьИтогиПоСтраницеТТН(ИтоговыеСуммы);
					
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ОбластьЗаголовокТаблицы.Параметры.НомерСтраницы = "Страница " + НомерСтраницы;
					ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
				КонецЕсли;
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			РассчитатьИтоговыеСуммыТТН(ИтоговыеСуммы, ДанныеСтроки);

		КонецЦикла;
		
		// Выводим итоги по последней странице
		ОбластьИтоговПоСтранице = Макет.ПолучитьОбласть("ИтогоПоСтранице");
		ОбластьИтоговПоСтранице.Параметры.Заполнить(ИтоговыеСуммы);
		
		ТабличныйДокумент.Вывести(ОбластьИтоговПоСтранице);
		
		// Выводим итоги по документу в целом
		ОбластьМакета = Макет.ПолучитьОбласть("Всего");
		ОбластьМакета.Параметры.Заполнить(ИтоговыеСуммы);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		// Выводим подвал документа
		ДобавитьИтоговыеДанныеПодвалаТТН(ИтоговыеСуммы, ДанныеСтроки.Номер);
		ЗаполнитьРеквизитыПодвалаТТН(СсылкаНаОбъект, ИтоговыеСуммы, Макет, ТабличныйДокумент,КоличествоСтрок);
		
		МассивВыводимыхОбластей.Очистить();
				
		ЗаполнитьРеквизитыТранспортногоРазделаТТН(СсылкаНаОбъект, Макет, ОбластьТранспортногоРаздела);
		
		МассивВыводимыхОбластей.Добавить(ОбластьТранспортногоРаздела);
		МассивВыводимыхОбластей.Добавить(ОбластьСведенийОГрузе);
		МассивВыводимыхОбластей.Добавить(ОбластьПодвалаСведенийОГрузе);
		МассивВыводимыхОбластей.Добавить(ОбластьПогрузочныеОперации);
		МассивВыводимыхОбластей.Добавить(ОбластьПрочихСведений);
		
		Если Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьТранспортногоРаздела);
		
		ЗаполнитьРеквизитыСведенийОГрузе(СсылкаНаОбъект, Макет, ОбластьСведенийОГрузе, ИтоговыеСуммы);
		ТабличныйДокумент.Вывести(ОбластьСведенийОГрузе);

		ЗаполнитьРеквизитыПодвалаСведенийОГрузе(СсылкаНаОбъект, Макет, ОбластьПодвалаСведенийОГрузе);
		ТабличныйДокумент.Вывести(ОбластьПодвалаСведенийОГрузе);
		
		ТабличныйДокумент.Вывести(ОбластьПогрузочныеОперации);
		ТабличныйДокумент.Вывести(ОбластьПрочихСведений);
	 
	КонецЦикла;
	
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	Возврат ТабличныйДокумент;
		
КонецФункции

Функция СформироватьПечатнуюФормуНакладная(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент 							= Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерКолонтитулаСверху 	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу 	= 0;
	ТабличныйДокумент.ПолеСлева					= 25;
	ТабличныйДокумент.АвтоМасштаб 				= Истина;
	ТабличныйДокумент.ОриентацияСтраницы 		= ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Накладная";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТранспортировкаПродукции.Накладная");
	
	ОбластьШапка		= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока		= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого		= Макет.ПолучитьОбласть("Итого");
	ОбластьПодвала 		= Макет.ПолучитьОбласть("Подвал");
	ОбластьРазделителя	= Макет.ПолучитьОбласть("Разделитель");
	ОбластьОбщиеИтоги 	= Макет.ПолучитьОбласть("ИтогиОбщие");
	   	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("НаименованиеОрганизации",		pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Организация,"НаименованиеПолное"));
		СтруктураДанныхДляПечати.Вставить("ДатаОтгрузки",					СсылкаНаОбъект.Дата);
		СтруктураДанныхДляПечати.Вставить("НомерМашины",					СсылкаНаОбъект.ТранспортноеСредство.НомерТС);	
		СтруктураДанныхДляПечати.Вставить("РеквизитыДокумента",				"№ "+СсылкаНаОбъект.Номер+" от "+Формат(СсылкаНаОбъект.Дата,"ДЛФ=ДД"));	
		
		ОбластьШапка.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьШапка);
				 
		ТабличнаяЧасть 		= СсылкаНаОбъект.Продукция;
				
		СводнаяТаблицаПоПартиям = Новый ТаблицаЗначений;
		СводнаяТаблицаПоПартиям.Колонки.Добавить("Партия", 							Новый ОписаниеТипов("СправочникСсылка.ПартииПродукции"));
		СводнаяТаблицаПоПартиям.Колонки.Добавить("ДанныеТабличнойЧастиПоПартии",	Новый ОписаниеТипов("ТаблицаЗначений"));
		
		Для Каждого СтрокаТаб Из ТабличнаяЧасть Цикл
			
			ДанныеТабличнойЧасти = Новый ТаблицаЗначений;
			ДанныеТабличнойЧасти.Колонки.Добавить("НомерМешкаВПартии",	ОбщегоНазначения.ОписаниеТипаЧисло(3,0));
			ДанныеТабличнойЧасти.Колонки.Добавить("НомерПломбы",		ОбщегоНазначения.ОписаниеТипаСтрока(10));
			ДанныеТабличнойЧасти.Колонки.Добавить("ВесБруттоВлажный",	ОбщегоНазначения.ОписаниеТипаЧисло(10,3));
			
			НоваяСтрокаТаблицыДанных 					= ДанныеТабличнойЧасти.Добавить();
			НоваяСтрокаТаблицыДанных.НомерМешкаВПартии 	= СтрокаТаб.НомерМестаВПартии;
			НоваяСтрокаТаблицыДанных.НомерПломбы 		= СтрокаТаб.НомерПломбы;
			НоваяСтрокаТаблицыДанных.ВесБруттоВлажный 	= СтрокаТаб.ВесБруттоВлажный;
			
			СтрокаПартии = СводнаяТаблицаПоПартиям.Найти(СтрокаТаб.ПартияПродукции);
			Если СтрокаПартии = Неопределено Тогда 
				НоваяСтрокаСводнойТаблицы = СводнаяТаблицаПоПартиям.Добавить();
				НоваяСтрокаСводнойТаблицы.Партия = СтрокаТаб.ПартияПродукции;
				НоваяСтрокаСводнойТаблицы.ДанныеТабличнойЧастиПоПартии = ДанныеТабличнойЧасти;
			Иначе 
				ТаблицаДанныхВнутри = СтрокаПартии.ДанныеТабличнойЧастиПоПартии;
				ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеТабличнойЧасти,ТаблицаДанныхВнутри);
				СтрокаПартии.ДанныеТабличнойЧастиПоПартии = ТаблицаДанныхВнутри;				
			КонецЕсли;
						
		КонецЦикла;
		
		
		КолМешковПоНакладной 	= 0;
		ИтогоВесПоНакладной 	= 0;
	
		
		Для Каждого СтрокаСводнойТаблицы Из СводнаяТаблицаПоПартиям Цикл
			
			Ном 				= 0;
			ИтогоВес 			= 0;
			ТекущаяПартия		= СтрокаСводнойТаблицы.Партия;
			ТаблицаДанных 		= СтрокаСводнойТаблицы.ДанныеТабличнойЧастиПоПартии;
			КоличествоМешков	= ТаблицаДанных.Количество();
			КолМешковПоНакладной = КолМешковПоНакладной + КоличествоМешков;
			
			ТабличныйДокумент.Вывести(ОбластьРазделителя);
			
			ОбластьЗаголовок.Параметры.НомПартии = ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(СокрЛП(ТекущаяПартия.Код));
			ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
			Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
			
				ИтогоВес	= ИтогоВес+СтрокаДанных.ВесБруттоВлажный;
				ИтогоВесПоНакладной = ИтогоВесПоНакладной + СтрокаДанных.ВесБруттоВлажный;
				Ном 		= Ном+1;
								
				СтруктураДанныхДляПечати = новый Структура;
				СтруктураДанныхДляПечати.Вставить("Ном",			Ном);
				СтруктураДанныхДляПечати.Вставить("НомПломбы",		СтрокаДанных.НомерПломбы);
				СтруктураДанныхДляПечати.Вставить("НомМешка",		СтрокаДанных.НомерМешкаВПартии);	
				СтруктураДанныхДляПечати.Вставить("Вес",			СтрокаДанных.ВесБруттоВлажный);
				
				ОбластьСтрока.Параметры.Заполнить(СтруктураДанныхДляПечати);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;
			
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("КолМешков",	КоличествоМешков);
			СтруктураДанныхДляПечати.Вставить("ИтогоВес",	ИтогоВес);
			
			ОбластьИтого.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьИтого);
			
		КонецЦикла;
		
		ОбластьОбщиеИтоги.Параметры.КолМешковПоНакладной = КолМешковПоНакладной;
		ОбластьОбщиеИтоги.Параметры.ИтогоВесПоНакладной = ИтогоВесПоНакладной;
		ТабличныйДокумент.Вывести(ОбластьОбщиеИтоги);
		
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ФИООтпустил"			,НаименованиеСотрудникаДляВизы(СсылкаНаОбъект.ВизаОтправителя));
		СтруктураДанныхДляПечати.Вставить("ДолжностьОтпустил"	,СсылкаНаОбъект.ВизаОтправителя.Должность.Наименование);
		СтруктураДанныхДляПечати.Вставить("Примечание"			, СокрЛП(СсылкаНаОбъект.Комментарий));
		СтруктураДанныхДляПечати.Вставить("ДолжностьПолучил"	,"Водитель");
		СтруктураДанныхДляПечати.Вставить("ФИОПолучил"			,СокрЛП(СсылкаНаОбъект.Водитель.Наименование));
		
		
		                                                    
		ОбластьПодвала.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвала);
				
	КонецЦикла;
		
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	Возврат ТабличныйДокумент;

КонецФункции

Функция СформироватьПечатнуюФормуНакладнаяЖД(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент 							= Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерКолонтитулаСверху 	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу 	= 0;
	ТабличныйДокумент.ПолеСлева					= 25;
	ТабличныйДокумент.АвтоМасштаб 				= Истина;
	ТабличныйДокумент.ОриентацияСтраницы 		= ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Накладная_ЖД";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТранспортировкаПродукции.Накладная_ЖД");
	
	ОбластьШапка		= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока		= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого		= Макет.ПолучитьОбласть("Итого");
	ОбластьПодвала 		= Макет.ПолучитьОбласть("Подвал");
	   	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("НаименованиеОрганизации",		pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Организация,"НаименованиеПолное"));
		СтруктураДанныхДляПечати.Вставить("РеквизитыДокумента",				"№ "+СсылкаНаОбъект.Номер+" от "+Формат(СсылкаНаОбъект.Дата,"ДЛФ=ДД"));	
		СтруктураДанныхДляПечати.Вставить("РеквизитыЗагрузки",				"Загрузка П/В № "+СсылкаНаОбъект.ТранспортноеСредство.НомерТС+" Дата: "+Формат(СсылкаНаОбъект.Дата,"ДЛФ=Д")+" г.");	
		
		ОбластьШапка.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		 
		ТабличнаяЧасть 		= СсылкаНаОбъект.Продукция.Выгрузить();
		КоличествоМешков	= ТабличнаяЧасть.Количество();
		ИтогоВес 			= 0;
		Ном 				= 0;
		
		ТабличнаяЧасть.Сортировать("ПартияПродукции");
		
		Для Каждого СтрокаТаб Из ТабличнаяЧасть Цикл
			
			ТекущаяПартия	= СтрокаТаб.ПартияПродукции;
			ИтогоВес		= ИтогоВес+СтрокаТаб.ВесБруттоВлажный;
			Ном 			= Ном+1;
			
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("Ном",		Ном);
			СтруктураДанныхДляПечати.Вставить("НомПартии",	ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(СокрЛП(ТекущаяПартия.Код)));
			СтруктураДанныхДляПечати.Вставить("НомПломбы",	СтрокаТаб.НомерПломбы);	
			СтруктураДанныхДляПечати.Вставить("НомМешка",	СтрокаТаб.НомерМестаВПартии);	
			СтруктураДанныхДляПечати.Вставить("Вес",		СтрокаТаб.ВесБруттоВлажный);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрока);

		КонецЦикла;
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ИтогоМешков",КоличествоМешков);
		СтруктураДанныхДляПечати.Вставить("ИтогоВес",	ИтогоВес);
		
		ОбластьИтого.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ФИООтпустил",		НаименованиеСотрудникаДляВизы(СсылкаНаОбъект.ВизаОтправителя));
		СтруктураДанныхДляПечати.Вставить("ДолжностьОтпустил",	СсылкаНаОбъект.ВизаОтправителя.Должность.Наименование);
				
		ОбластьПодвала.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвала);

	КонецЦикла;
		
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	Возврат ТабличныйДокумент;

КонецФункции

Функция СформироватьПечатнуюФормуНакладнаяЖД_Реализация(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент 							= Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерКолонтитулаСверху 	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу 	= 0;
	ТабличныйДокумент.ПолеСлева					= 25;
	ТабличныйДокумент.АвтоМасштаб 				= Истина;
	ТабличныйДокумент.ОриентацияСтраницы 		= ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Накладная_ЖД_вн";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ТранспортировкаПродукции.Накладная_ЖД_Реализация");
	
	ОбластьШапка		= Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
	ОбластьСтрока		= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого		= Макет.ПолучитьОбласть("Итого");
	ОбластьПодвала 		= Макет.ПолучитьОбласть("Подвал");
	   	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("НаименованиеОрганизации",		pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Организация,"НаименованиеПолное"));
		СтруктураДанныхДляПечати.Вставить("РеквизитыДокумента",				"№ "+СсылкаНаОбъект.Номер+" от "+Формат(СсылкаНаОбъект.Дата,"ДЛФ=ДД"));	
		СтруктураДанныхДляПечати.Вставить("РеквизитыЗагрузки",				"Загрузка П/В № "+СсылкаНаОбъект.ТранспортноеСредство.НомерТС+" Дата: "+Формат(СсылкаНаОбъект.Дата,"ДЛФ=Д")+" г.");	
		
		ОбластьШапка.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		 
		ТабличнаяЧасть 		= СсылкаНаОбъект.Продукция.Выгрузить();
		КоличествоМешков	= ТабличнаяЧасть.Количество();
		ИтогоВес 			= 0;
		Ном 				= 0;
		
		Для Каждого СтрокаТаб Из ТабличнаяЧасть Цикл
			
			ТекущаяПартия	= СтрокаТаб.ПартияПродукции;
			ИтогоВес		= ИтогоВес+СтрокаТаб.ВесБруттоВлажный;
			Ном 			= Ном+1;
			
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("Ном",		Ном);
			СтруктураДанныхДляПечати.Вставить("НомПартии",	ПрефиксацияОбъектовКлиентСервер.УдалитьЛидирующиеНулиИзНомераОбъекта(СокрЛП(ТекущаяПартия.Код)));
			СтруктураДанныхДляПечати.Вставить("НомПломбы",	СтрокаТаб.НомерПломбы);	
			СтруктураДанныхДляПечати.Вставить("НомМешка",	СтрокаТаб.НомерМестаВПартии);	
			СтруктураДанныхДляПечати.Вставить("Вес",		СтрокаТаб.ВесБруттоВлажный);
			СтруктураДанныхДляПечати.Вставить("НомерМестаКонтракта",		СтрокаТаб.НомерМестаВКонтракте);
			
			ОбластьСтрока.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрока);

		КонецЦикла;
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ИтогоМешков",КоличествоМешков);
		СтруктураДанныхДляПечати.Вставить("ИтогоВес",	ИтогоВес);
		
		ОбластьИтого.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьИтого);
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ФИООтпустил",		НаименованиеСотрудникаДляВизы(СсылкаНаОбъект.ВизаОтправителя));
		СтруктураДанныхДляПечати.Вставить("ДолжностьОтпустил",	СсылкаНаОбъект.ВизаОтправителя.Должность.Наименование);
				
		ОбластьПодвала.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвала);

	КонецЦикла;
		
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	Возврат ТабличныйДокумент;

КонецФункции
