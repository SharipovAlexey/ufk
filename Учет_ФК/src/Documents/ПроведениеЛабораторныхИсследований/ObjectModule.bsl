
Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ДанныеЛабораторныхИсследований
	Движения.ДанныеЛабораторныхИсследований.Записывать = Истина;
	Движение = Движения.ДанныеЛабораторныхИсследований.Добавить();
	Движение.ЛабораторнаяПроба = ЛабораторнаяПроба;
	Движение.Партия = ПартияПродукции;
	Движение.СодержаниеAu = СодержаниеAu;
	Движение.ПоказательТочностиСодержанияAu = ПоказательТочностиСодержанияAu;
	Движение.СодержаниеAg = СодержаниеAg;
	Движение.ПоказательТочностиСодержанияAg = ПоказательТочностиСодержанияAg;
	Движение.ПримесиSобщ = ПримесиSобщ;
	Движение.ПоказательТочностиПримесиSобщ = ПоказательТочностиПримесиSобщ;
	Движение.ПримесиCорг = ПримесиCорг;
	Движение.ПоказательТочностиПримесиCорг = ПоказательТочностиПримесиCорг;
	Движение.ПримесиAs = ПримесиAs;
	Движение.ПоказательТочностиПрмесиAs = ПоказательТочностиПрмесиAs;
	Движение.ПримесиFe = ПримесиFe;
	Движение.ПоказательТочностиПрмесиFe = ПоказательТочностиПрмесиFe;

	//регистр Данные содержаний в транспортировочных местах
	
	Движения.ДанныеСодержанияДМИПримесейВТранспортировочныхМестах.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВыпускПродукцииТранспортировочныеМеста.ТранспортировочноеМесто КАК ТранспортировочноеМесто,
		|	ВыпускПродукцииТранспортировочныеМеста.ВесНеттоСухой КАК ВесНеттоСухой
		|ИЗ
		|	Документ.ВыпускПродукции.ТранспортировочныеМеста КАК ВыпускПродукцииТранспортировочныеМеста
		|ГДЕ
		|	ВыпускПродукцииТранспортировочныеМеста.Ссылка.Проведен = ИСТИНА
		|	И ВыпускПродукцииТранспортировочныеМеста.Ссылка.Партия = &Партия";
	Запрос.УстановитьПараметр("Партия", ПартияПродукции);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ДанныеСодержанияДМИПримесейВТранспортировочныхМестах.Добавить();
		Движение.ТранспортировочноеМесто = ВыборкаДетальныеЗаписи.ТранспортировочноеМесто;
		Движение.КоличествоAu 	= Окр(СодержаниеAu * ВыборкаДетальныеЗаписи.ВесНеттоСухой/1000,2,РежимОкругления.Окр15как20);
		Движение.КоличествоAg  	= Окр(СодержаниеAg * ВыборкаДетальныеЗаписи.ВесНеттоСухой/1000,2,РежимОкругления.Окр15как20);
		Движение.КоличествоAs   = Окр(ВыборкаДетальныеЗаписи.ВесНеттоСухой * ПримесиAs/100,3,РежимОкругления.Окр15как20);
		Движение.КоличествоCорг = Окр(ВыборкаДетальныеЗаписи.ВесНеттоСухой * ПримесиCорг/100,3,РежимОкругления.Окр15как20);
		Движение.КоличествоSобщ = Окр(ВыборкаДетальныеЗаписи.ВесНеттоСухой * ПримесиSобщ/100,3,РежимОкругления.Окр15как20);
		Движение.КоличествоFe   = Окр(ВыборкаДетальныеЗаписи.ВесНеттоСухой * ПримесиFe/100,3,РежимОкругления.Окр15как20);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = Справочники.Организации.ОрганизацияПоУмолчанию(ПараметрыСеанса.ТекущийПользователь.Наименование);	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры
