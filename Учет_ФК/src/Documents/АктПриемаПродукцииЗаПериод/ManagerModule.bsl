// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#Область ПрограммныйИнтерфейс
// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктПриема";
	КомандаПечати.Представление = НСтр("ru = 'Акт приемки продукции'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок       = 10;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктНарушений";
	КомандаПечати.Представление = НСтр("ru = 'Акт выявленных нарушений'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок       = 11;
	
КонецПроцедуры
#КонецОбласти


Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктПриема") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктПриема", "Акт приемки продукции",
			СформироватьПечатнуюФормуАктаПриемаПродукции(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,);
	ИначеЕсли УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктНарушений") Тогда 
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АктНарушений", "Акт выявленных нарушений",
			СформироватьПечатнуюФормуАктаВыявленныхНарушений(МассивОбъектов, ОбъектыПечати, ПараметрыПечати),,);
	КонецЕсли;
	
КонецПроцедуры


Функция СформироватьПечатнуюФормуАктаПриемаПродукции(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент 							= Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерКолонтитулаСверху 	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу 	= 0;
	ТабличныйДокумент.ПолеСлева					= 25;
	ТабличныйДокумент.АвтоМасштаб 				= Истина;
	ТабличныйДокумент.ОриентацияСтраницы 		= ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктПриемапродукции";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АктПриемаПродукцииЗаПериод.ПФ_MXL_АктПриемкиПродукции");
	
	ОбластьШапка			= Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаКомисcии	= Макет.ПолучитьОбласть("СтрокаКомиссии");
	ОбластьПодвал  			= Макет.ПолучитьОбласть("Подвал");
	   	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("НаименованиеОрганизации",		pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Организация,"НаименованиеПолное"));
		СтруктураДанныхДляПечати.Вставить("ДатаДокумента",					СсылкаНаОбъект.Дата);
		СтруктураДанныхДляПечати.Вставить("НомерНаПечать",					pm_ОбщегоНазначения.НомерНаПечать(СсылкаНаОбъект.Номер));	
		СтруктураДанныхДляПечати.Вставить("ПредставлениеПолучателя",		СокрЛП(СсылкаНаОбъект.Грузополучатель.НаименованиеПолное));	
		
		ПереченьКомисси = " ";
		Для каждого СтрокаЧленКомиссии из СсылкаНаОбъект.Комиссия Цикл
			ПереченьКомисси = ПереченьКомисси + СокрЛП(СтрокаЧленКомиссии.Сотрудник.Должность) + " " + НаименованиеСотрудникаДляВизы(СтрокаЧленКомиссии.Сотрудник) +", ";
		КонецЦикла;
		
		КоличествоПринятыхМест = 0;
		Для каждого СтрокаДокументПоступления из СсылкаНаОбъект.ДокументыПоступления Цикл 
			КоличествоПринятыхМест = КоличествоПринятыхМест + СтрокаДокументПоступления.ДокуменПоступленияИзТранспортировки.Продукция.Количество(); 	
		КонецЦикла;
		
		ТекстовкаАкта = "Комиссия в составе:" + ПереченьКомисси +" составила настоящий акт, удостоверяющий, что "+
		Формат(СсылкаНаОбъект.Дата,"ДФ=dd.MM.yyyy")+ " г. приняли "+КоличествоПринятыхМест+" биг-бэгов с массой брутто "+СсылкаНаОбъект.ДокументыПоступления.Итог("ВесПродукцииПоДокументу")/1000+" т. флотоконцентрата ООО «Маломырский рудник».";
		
		СтруктураДанныхДляПечати.Вставить("ТекстовкаАкта",ТекстовкаАкта);
		СтруктураДанныхДляПечати.Вставить("ДополнительнаяИнформация",СокрЛП(СсылкаНаОбъект.ДополнительнаяИнформацияАктаОПриемке));
				
		ОбластьШапка.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("СлужбаБезопасности",Ложь);
		МассивСтрокКомиссии = СсылкаНаОбъект.Комиссия.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаТаблицыКомиссии из МассивСтрокКомиссии Цикл 
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("ФИОЧленаКомиссии"			,НаименованиеСотрудникаДляВизы(СтрокаТаблицыКомиссии.Сотрудник));
			СтруктураДанныхДляПечати.Вставить("ДолжностьЧленаКомисси"		,СтрокаТаблицыКомиссии.Сотрудник.Должность.Наименование);
			ОбластьСтрокаКомисcии.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрокаКомисcии);
		КонецЦикла;
		
		
//Выводим область и визу службы безопасности
        СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ПредставлениеДаты",Формат(СсылкаНаОбъект.Дата,"ДФ='MM.yyyy ""г""'"));
        ОбластьПодвал.Параметры.Заполнить(СтруктураДанныхДляПечати);
        ТабличныйДокумент.Вывести(ОбластьПодвал);

		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("СлужбаБезопасности",Истина);
		МассивСтрокКомиссии = СсылкаНаОбъект.Комиссия.НайтиСтроки(СтруктураПоиска);
		Для каждого СтрокаТаблицыКомиссии из МассивСтрокКомиссии Цикл 
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("ФИОЧленаКомиссии"			,НаименованиеСотрудникаДляВизы(СтрокаТаблицыКомиссии.Сотрудник));
			СтруктураДанныхДляПечати.Вставить("ДолжностьЧленаКомисси"		,СтрокаТаблицыКомиссии.Сотрудник.Должность.Наименование);
			ОбластьСтрокаКомисcии.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрокаКомисcии);
		КонецЦикла;
	КонецЦикла;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция НаименованиеСотрудникаДляВизы(Сотрудник)
	
	Если СтрДлина(СокрЛП(Сотрудник.ПредставлениеДляПечати))>0 Тогда
		Возврат СокрЛП(Сотрудник.ПредставлениеДляПечати);
	Иначе 
		Возврат СокрЛП(Сотрудник.Наименование);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция СформироватьПечатнуюФормуАктаВыявленныхНарушений(МассивОбъектов, ОбъектыПечати, ПараметрыПечати) Экспорт
	
	ТабличныйДокумент 							= Новый ТабличныйДокумент;
	ТабличныйДокумент.РазмерКолонтитулаСверху 	= 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу 	= 0;
	ТабличныйДокумент.ПолеСлева					= 25;
	ТабличныйДокумент.АвтоМасштаб 				= Истина;
	ТабличныйДокумент.ОриентацияСтраницы 		= ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктВыявленныхНарушений";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.АктПриемаПродукцииЗаПериод.ПФ_MXL_АктПВыявленныхНарушенийТранспортировки");
	
	ОбластьШапка			= Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрокаКомисcии	= Макет.ПолучитьОбласть("СтрокаКомиссии");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ОбластьСтрокаТаблицы 	= Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал			= Макет.ПолучитьОбласть("Подвал");
	   	
	Для Каждого СсылкаНаОбъект Из МассивОбъектов Цикл 
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("НаименованиеОрганизации",		pm_ОбщегоНазначения.ОписаниеОрганизации(СсылкаНаОбъект.Организация,"НаименованиеПолное"));
		СтруктураДанныхДляПечати.Вставить("ДатаДокумента",					СсылкаНаОбъект.Дата);
		СтруктураДанныхДляПечати.Вставить("НомерНаПечать",					pm_ОбщегоНазначения.НомерНаПечать(СсылкаНаОбъект.Номер));	
		СтруктураДанныхДляПечати.Вставить("ПредставлениеПолучателя",		СокрЛП(СсылкаНаОбъект.Грузополучатель.НаименованиеПолное));	
		
		ПереченьКомисси = " ";
		Для каждого СтрокаЧленКомиссии из СсылкаНаОбъект.Комиссия Цикл
			ПереченьКомисси = ПереченьКомисси + СокрЛП(СтрокаЧленКомиссии.Сотрудник.Должность) + " " + НаименованиеСотрудникаДляВизы(СтрокаЧленКомиссии.Сотрудник) +", ";
		КонецЦикла;
		
		КоличествоПринятыхМест = 0;
		Для каждого СтрокаДокументПоступления из СсылкаНаОбъект.ДокументыПоступления Цикл 
			КоличествоПринятыхМест = КоличествоПринятыхМест + СтрокаДокументПоступления.ДокуменПоступленияИзТранспортировки.Продукция.Количество(); 	
		КонецЦикла;
		
		ТекстовкаАкта = "Комиссия в составе:" + ПереченьКомисси +" составила настоящий акт, удостоверяющий, что "+
		Формат(СсылкаНаОбъект.Дата,"ДФ=dd.MM.yyyy")+ " г. при приеме флотоконцентрата ООО «Маломырский рудник» был выявлен ряд нарушений:";
		
		СтруктураДанныхДляПечати.Вставить("ТекстовкаАкта",ТекстовкаАкта);
		
		ОбластьШапка.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоступлениеПродукцииИзТранспортировкиПродукция.ВыявленноеНарушение КАК ВыявленноеНарушение,
			|	ПоступлениеПродукцииИзТранспортировкиПродукция.ПартияПродукции КАК ПартияПродукции,
			|	ПоступлениеПродукцииИзТранспортировкиПродукция.НомерМестаВПартии КАК НомерМестаВПартии
			|ИЗ
			|	Документ.ПоступлениеПродукцииИзТранспортировки.Продукция КАК ПоступлениеПродукцииИзТранспортировкиПродукция
			|ГДЕ
			|	ПоступлениеПродукцииИзТранспортировкиПродукция.Ссылка В
			|			(ВЫБРАТЬ
			|				АктПриемаПродукцииЗаПериодДокументыПоступления.ДокуменПоступленияИзТранспортировки КАК ДокуменПоступленияИзТранспортировки
			|			ИЗ
			|				Документ.АктПриемаПродукцииЗаПериод.ДокументыПоступления КАК АктПриемаПродукцииЗаПериодДокументыПоступления
			|			ГДЕ
			|				АктПриемаПродукцииЗаПериодДокументыПоступления.Ссылка = &Ссылка)
			|	И ПоступлениеПродукцииИзТранспортировкиПродукция.ВыявленноеНарушение <> ЗНАЧЕНИЕ(Справочник.ВидыНарушенийПриТранспортировке.ПустаяСсылка)
			|ИТОГИ ПО
			|	ВыявленноеНарушение,
			|	ПартияПродукции";
		
		Запрос.УстановитьПараметр("Ссылка", СсылкаНаОбъект);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаВыявленноеНарушение = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		НПП = 1;
		Пока ВыборкаВыявленноеНарушение.Следующий() Цикл
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("НПП",НПП);
			СтруктураДанныхДляПечати.Вставить("НаименованиеНарушения",СокрЛП(ВыборкаВыявленноеНарушение.ВыявленноеНарушение.Наименование));
			
			СтрокаОписания = "";
			ВыборкаПартияПродукции = ВыборкаВыявленноеНарушение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПартияПродукции.Следующий() Цикл
				СтрокаОписания = СтрокаОписания + " Партия №: " + ВыборкаПартияПродукции.ПартияПродукции + " Место: ";
				переченьМест = "";
				ВыборкаДетальныеЗаписи = ВыборкаПартияПродукции.Выбрать();
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					переченьМест = переченьМест + ВыборкаДетальныеЗаписи.НомерМестаВПартии + ", ";
				КонецЦикла;
				СтрокаОписания = СтрокаОписания + переченьМест;
			КонецЦикла;
			СтруктураДанныхДляПечати.Вставить("СписокПартийиМест",СтрокаОписания);
			ОбластьСтрокаТаблицы.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			
			НПП = НПП + 1;
		КонецЦикла;
		
		СтруктураДанныхДляПечати = новый Структура;
		СтруктураДанныхДляПечати.Вставить("ДополнительнаяИнформация",СокрЛП(СсылкаНаОбъект.ДополнительнаяИнформацияАктаВыявленныхнарушений));		
		ОбластьПодвал.Параметры.Заполнить(СтруктураДанныхДляПечати);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		Для каждого СтрокаТаблицыКомиссии из СсылкаНаОбъект.Комиссия Цикл 
			СтруктураДанныхДляПечати = новый Структура;
			СтруктураДанныхДляПечати.Вставить("ФИОЧленаКомиссии"			,НаименованиеСотрудникаДляВизы(СтрокаТаблицыКомиссии.Сотрудник));
			СтруктураДанныхДляПечати.Вставить("ДолжностьЧленаКомисси"		,СтрокаТаблицыКомиссии.Сотрудник.Должность.Наименование);
			
			ОбластьСтрокаКомисcии.Параметры.Заполнить(СтруктураДанныхДляПечати);
			ТабличныйДокумент.Вывести(ОбластьСтрокаКомисcии);
		КонецЦикла;
	КонецЦикла;
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	
	Возврат ТабличныйДокумент;

КонецФункции
