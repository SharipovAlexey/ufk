
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НастройкиКлиентскогоПриложения = ХранилищеСистемныхНастроек.Загрузить("Общее/НастройкиКлиентскогоПриложения");
	
	Если ТипЗнч(НастройкиКлиентскогоПриложения) <> Тип("НастройкиКлиентскогоПриложения") Тогда
		НастройкиКлиентскогоПриложения = Новый НастройкиКлиентскогоПриложения;
		НастройкиКлиентскогоПриложения.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2;
		НастройкиКлиентскогоПриложения.ВариантМасштабаФормКлиентскогоПриложения = ВариантМасштабаФормКлиентскогоПриложения.Авто;
		НастройкиКлиентскогоПриложения.ОтображатьПанелиНавигацииИДействий = Истина;
		НастройкиКлиентскогоПриложения.ОтображатьПанельРазделов = Истина;
		НастройкиКлиентскогоПриложения.РежимОткрытияФормПриложения = РежимОткрытияФормПриложения.Закладки;
		НастройкиКлиентскогоПриложения.СкоростьКлиентскогоСоединения = СкоростьКлиентскогоСоединения.Обычная;
	КонецЕсли;
	
	ствВариантыИнтерфейса = Новый Соответствие;
	ствВариантыИнтерфейса.Вставить(ВариантИнтерфейсаКлиентскогоПриложения.Версия8_2, "Версия8_2");
	ствВариантыИнтерфейса.Вставить(ВариантИнтерфейсаКлиентскогоПриложения.Такси, "Такси");
	
	ствВариантМасштабаФорм = Новый Соответствие;
	ствВариантМасштабаФорм.Вставить(ВариантМасштабаФормКлиентскогоПриложения.Авто, "Авто");
	ствВариантМасштабаФорм.Вставить(ВариантМасштабаФормКлиентскогоПриложения.Обычный, "Обычный");
	ствВариантМасштабаФорм.Вставить(ВариантМасштабаФормКлиентскогоПриложения.Компактный, "Компактный");
	
	ствРежимОткрытияФорм = Новый Соответствие;
	ствРежимОткрытияФорм.Вставить(РежимОткрытияФормПриложения.Закладки, "Закладки");
	ствРежимОткрытияФорм.Вставить(РежимОткрытияФормПриложения.ОтдельныеОкна, "ОтдельныеОкна");
	
	ВариантИнтерфейса = ствВариантыИнтерфейса[НастройкиКлиентскогоПриложения.ВариантИнтерфейсаКлиентскогоПриложения];
	ВариантМасштабаФорм = ствВариантМасштабаФорм[НастройкиКлиентскогоПриложения.ВариантМасштабаФормКлиентскогоПриложения];
	РежимОткрытияФорм = ствРежимОткрытияФорм[НастройкиКлиентскогоПриложения.РежимОткрытияФормПриложения];
	ОтображатьПанелиНавигацииИДействий = НастройкиКлиентскогоПриложения.ОтображатьПанелиНавигацииИДействий;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантИнтерфейсаПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимОткрытияФормПриИзменении(Элемент)
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеВидимостью()
	
	Если ВРег(ВариантИнтерфейса) = ВРег("Такси") Тогда
		Элементы.РежимОткрытияФорм.Видимость = Ложь;
		Элементы.ВариантМасштабаФорм.Видимость = Истина;
		Элементы.ОтображатьПанелиНавигацииИДействий.Видимость = Ложь;
	Иначе
		Элементы.РежимОткрытияФорм.Видимость = Истина;
		Элементы.ВариантМасштабаФорм.Видимость = Ложь;
		Если ВРег(РежимОткрытияФорм) = ВРег("Закладки") Тогда
			Элементы.ОтображатьПанелиНавигацииИДействий.Видимость = Истина;
		Иначе
			Элементы.ОтображатьПанелиНавигацииИДействий.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройки(Команда)
		
	ПрименитьНастройкиНаСервере();
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкиНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	НастройкиКлиентскогоПриложения.ВариантИнтерфейсаКлиентскогоПриложения = ВариантИнтерфейсаКлиентскогоПриложения[ВариантИнтерфейса];
	НастройкиКлиентскогоПриложения.ВариантМасштабаФормКлиентскогоПриложения = ВариантМасштабаФормКлиентскогоПриложения[ВариантМасштабаФорм];
		
	Если ВРег(ВариантИнтерфейса) = ВРег("Такси") Тогда
		НастройкиКлиентскогоПриложения.РежимОткрытияФормПриложения = РежимОткрытияФормПриложения.ОтдельныеОкна;
	Иначе
		НастройкиКлиентскогоПриложения.РежимОткрытияФормПриложения = РежимОткрытияФормПриложения[РежимОткрытияФорм];
		Если ВРег(РежимОткрытияФорм) = ВРег("Закладки") Тогда
			НастройкиКлиентскогоПриложения.ОтображатьПанелиНавигацииИДействий = ОтображатьПанелиНавигацииИДействий;
		КонецЕсли;
	КонецЕсли;
	
	ХранилищеСистемныхНастроек.Сохранить("Общее/НастройкиКлиентскогоПриложения", "", НастройкиКлиентскогоПриложения);
	
КонецПроцедуры







