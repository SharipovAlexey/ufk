// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает склад по умолчанию.
// Если в ИБ есть только один склад, который не помечена на удаление и не является предопределенной,
// то будет возвращена ссылка на него, иначе будет возвращена пустая ссылка.
//
// Возвращаемое значение:
//     СправочникСсылка.Склады - ссылка на склад.
//
Функция СкладПоУмолчанию(ИмяПользователяИБ = Неопределено) Экспорт
	ЗначениеПоУмолчанию = Справочники.Склады.ПустаяСсылка();
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.Склады) Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	   
	Если КоличествоСкладов() > 1 Тогда
		ОсновнойСклад = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиФК", "ОсновнойСклад", Истина);
		                                           		
		Если ТипЗнч(ОсновнойСклад) = Тип("СправочникСсылка.Склады") Тогда
			// Проверка наличия ссылки и прав доступа
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ОсновнойСклад", ОсновнойСклад);
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Склады.Ссылка КАК Склад
			|ИЗ
			|	Справочник.Склады КАК Склады
			|ГДЕ
			|	Склады.Ссылка = &ОсновнойСклад";
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ЗначениеПоУмолчанию = Выборка.Склад;
			КонецЕсли;
		КонецЕсли;
	Иначе
		   		
		ТекстЗапроса =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Склады.Ссылка КАК Склад
			|ИЗ
			|	Справочник.Склады КАК Склады";
		
		Если ОбщегоНазначенияКлиентСервер.РежимОтладки() Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|ГДЕ НЕ Склады.ПометкаУдаления";
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Результат = Запрос.Выполнить();
		Если НЕ Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Если Выборка.Следующий() Тогда
				ЗначениеПоУмолчанию = Выборка.Склад;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ЗначениеПоУмолчанию;
	
КонецФункции

// Возвращает количество элементов справочника склады.
// Не учитывает предопределенные и помеченные на удаление элементы.
//
// Возвращаемое значение:
//     Число - количество складов.
//
Функция КоличествоСкладов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Склады.Ссылка) КАК КоличествоСкладов
		|ИЗ
		|	Справочник.Склады КАК Склады";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.КоличествоСкладов;
	Иначе
		Возврат 0;
	КонецЕсли;
КонецФункции

